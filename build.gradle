buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
        jcenter()
    }
    dependencies {
        classpath "org.grails:grails-docs:$grailsVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
        classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.4.0"
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
        classpath "gradle.plugin.org.aim42:htmlSanityCheck:$htmlSanityCheckVersion"
        classpath "io.spring.nohttp:nohttp-gradle:0.0.4.RELEASE"
        classpath "io.micronaut.docs:micronaut-docs-asciidoc-extensions:$micronautDocsVersion"
        classpath "io.micronaut.docs:micronaut-docs-gradle-plugins:$micronautDocsVersion", {
            exclude module:'shadow'
            exclude module:'plugin-shadow'
        }
        classpath "org.gradle:test-retry-gradle-plugin:1.1.5"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.15.0' apply false
    id 'com.github.ben-manes.versions' version '0.28.0'
}

apply plugin: 'org.aim42.htmlSanityCheck'

version project.projectVersion

ext {
    distInstallDir = file("$buildDir/dist-tmp")
    homeBinDir = file("bin")
    homeLibDir = file("lib")
    homeSrcDir = file("src")
}

subprojects { Project subproject ->

    repositories {
        jcenter()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://oss.jfrog.org/oss-snapshot-local" }
    }

    ext {
        isConfiguration = false
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    apply plugin: "io.spring.nohttp"
    apply plugin:"groovy"
    apply plugin:"java-library"
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: "com.github.ben-manes.versions"    
    apply plugin: "org.gradle.test-retry"

    apply from: rootProject.file('gradle/license.gradle')

    version project.projectVersion
    group "io.micronaut"


    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    apply from:"https://raw.githubusercontent.com/micronaut-projects/micronaut-build/v${micronautBuildVersion}/publishing.gradle"


    jar {
        manifest {
            attributes('Automatic-Module-Name': "${subproject.group}.${subproject.name}".replaceAll('[^\\w\\.\\$_]', "_"))
            attributes('Implementation-Version': projectVersion)
            attributes('Implementation-Title': title)
        }
    }

    dependencyUpdates {
        rejectVersionIf { mod ->
            mod.candidate.version ==~ /.+(-|\.?)(b|M|RC)\d.*/ ||
                    ["alpha", "beta", "milestone"].any {
                        mod.candidate.version.toLowerCase(Locale.ENGLISH).contains(it) } ||
                    mod.candidate.group.startsWith('io.micronaut') || // managed by the micronaut version
                    mod.candidate.module.contains('htmlunit')
        }
        outputFormatter = { result ->
            if (!result.outdated.dependencies.isEmpty()) {
                def upgradeVersions = result.outdated.dependencies
                if (!upgradeVersions.isEmpty()) {
                    println "\nThe following dependencies have later ${revision} versions:"
                    upgradeVersions.each { dep ->
                        def currentVersion = dep.version
                        println " - ${dep.group}:${dep.name} [${currentVersion} -> ${dep.available[revision]}]"
                        if (dep.projectUrl != null) {
                            println "     ${dep.projectUrl}"
                        }
                    }
                    throw new GradleException('Abort, there are dependencies to update.')
                }
            }
        }
    }
    check.dependsOn('dependencyUpdates')

    bintray.publish = true

    tasks.withType(Test) {
        jvmArgs '-Duser.country=US'
        jvmArgs '-Duser.language=en'
        testLogging {
            exceptionFormat = 'full'
        }
        afterSuite {
            System.out.print(".")
            System.out.flush()
        }

        reports.html.enabled = !System.getenv("GITHUB_ACTIONS")
        reports.junitXml.enabled = !System.getenv("GITHUB_ACTIONS")

        systemProperty "micronaut.cloud.platform", "OTHER"

        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
        forkEvery = 100

        retry {
            maxRetries = System.getProperty("retry.max") !=null ? (System.getProperty("retry.max") as Integer) : (System.getenv("GITHUB_ACTIONS") ? 3 : 0)
            maxFailures = 20
            failOnPassedAfterRetry = false
       }
    }
    
    configurations {
        documentation
        all {
            resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                String group = details.requested.group
                if (group == 'org.codehaus.groovy') {
                    details.useVersion(groovyVersion)
                }
            }
        }
    }
    dependencies {
        annotationProcessor enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion")
        testAnnotationProcessor enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion")
        implementation enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion")
        api platform("io.micronaut:micronaut-bom:$micronautVersion")

        documentation "org.codehaus.groovy:groovy-templates:$groovyVersion"
        documentation "org.codehaus.groovy:groovy-dateutil:$groovyVersion"

        annotationProcessor "io.micronaut:micronaut-inject-java"
        annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:$micronautDocsVersion"
        annotationProcessor "io.micronaut:micronaut-graal"

        api "io.micronaut:micronaut-inject"

        testImplementation("org.spockframework:spock-core:${spockVersion}") {
            exclude module:'groovy-all'
        }

        testImplementation "io.micronaut:micronaut-inject-groovy"
        testImplementation "cglib:cglib-nodep:3.3.0"
        testImplementation "org.objenesis:objenesis:3.1"

        testRuntimeOnly "ch.qos.logback:logback-classic:1.2.3"
        testImplementation "org.codehaus.groovy:groovy-test:$groovyVersion"
    }

    groovydoc {
        classpath += project.configurations.documentation
    }

    task allDeps(type: DependencyReportTask) {}

    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = 8.32
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")

        // Per submodule
        maxErrors = 0
        maxWarnings = 10

        showViolations = true
    }

    checkstyleTest.enabled = false

    idea {
        module {
            outputDir file('build/classes/java/main')
            testOutputDir file('build/classes/groovy/test')
        }
    }

    tasks.withType(GroovyCompile) {
        groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
    }

    tasks.withType(JavaCompile){
        options.encoding = "UTF-8"
        options.compilerArgs.add('-parameters')
    }
}

apply from:"https://raw.githubusercontent.com/micronaut-projects/micronaut-docs/v${micronautDocsVersion}/gradle/docs.gradle"

htmlSanityCheck {
    sourceDir = new File("${rootProject.buildDir}/docs/")
    sourceDocuments = ["index.html"]

}

docs.finalizedBy(htmlSanityCheck)

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
