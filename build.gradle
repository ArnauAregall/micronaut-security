buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails:grails-docs:$grailsVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
    }
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

version project.projectVersion

ext {
    // commonBuild = "https://raw.githubusercontent.com/grails/grails-common-build/master"
    commonBuild = "https://raw.githubusercontent.com/grails/grails-common-build/3cad4c93ac433e1ae887c71835c94436086bb0ad/"
}

subprojects {

    version project.projectVersion

    ext {
        userOrg = "grails"
        isGrailsPlugin = false
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    group "org.particleframework"

    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    
    if(it.name.startsWith("examples")) {
        // apply plugin:"org.grails.grails-web"
    }
    else {
        apply from:"${commonBuild}/common-project.gradle"
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        if(!it.name.contains('test-suite')) {
            apply from:"${commonBuild}/common-publishing.gradle"
        }
    }

    tasks.withType(Test) {
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
    }
}

apply from:"${commonBuild}/common-docs.gradle"

// IDEA 2017.2 made a breaking change with IDE build output vs gradle build output
// [see https://youtrack.jetbrains.com/issue/IDEA-175172]
// the problem here is the gradle build uses <module-dir>/build/classes/main
// while IDEA uses <module-dir>/out/production/..
// As a result, compiling ast or java-inject module and using the IDEA test runner fails because the
// generated bean classes for injection are in <module-dir>/build, not <module-dir>/out
allprojects {
    apply plugin: 'idea'
    idea {
        module {
            outputDir file('build/classes/main')
            testOutputDir file('build/classes/test')
        }
    }
    if(project.convention.findPlugin(JavaPluginConvention)) {
        // Change the output directory for the main and test source sets back to the old path
        sourceSets.main.output.classesDir = new File(buildDir, "classes/main")
        sourceSets.test.output.classesDir = new File(buildDir, "classes/test")
    }
}